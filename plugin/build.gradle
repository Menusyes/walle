apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'

group = GROUP_ID
description = DESCRIPTION
version = VERSION_NAME

jar {
    manifest {
        attributes 'Walle-Version': version
    }
}


//本地依赖插件时使用
gradlePlugin {
    plugins {
        //插件名,每一个插件都可以有
        routerPlugin {
            id = group
            implementationClass = "com.meituan.android.walle.GradlePlugin"
        }
    }
}

sourceCompatibility = JavaVersion.VERSION_11
publishing {
    publications {
        maven(MavenPublication) {
            //插件名称
            artifactId = 'walle_plugin'
            from components.java
        }
    }
    //插件存储库
    repositories {
        maven {
            // 生成的插件位置
            url = uri('../repo')
        }
    }
}


dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation fileTree(dir: "./src/main/libs", include: ['*.jar'])
    implementation 'commons-io:commons-io:2.5'
    implementation 'commons-codec:commons-codec:1.11'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'com.android.tools.build:gradle:7.0.4'
    implementation 'org.json:json:20180813'// Groovy里有，但是如果这里不依赖报JsonException not found，还是强制依赖下吧。
    implementation project(':payload_writer')
    implementation 'com.google.code.gson:gson:2.9.0'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}